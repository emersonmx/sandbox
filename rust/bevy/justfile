os := "native"
_triple := if os == "windows" {
    "x86_64-pc-windows-gnu"
} else if os == "linux" {
    "x86_64-unknown-linux-gnu"
} else if os == "wasm" {
    "wasm32-unknown-unknown"
} else {
    ""
}
_target_triple := if _triple != "" { "--target " + _triple } else { "" }

export BEVY_ASSET_ROOT := justfile_directory()

[no-cd]
@build:
    cargo build --release {{_target_triple}}

[no-cd]
@run:
    #!/usr/bin/env bash
    set -euxo pipefail

    if [[ "{{os}}" == "windows" ]]; then
        export WSLENV=$WSLENV:BEVY_ASSET_ROOT/pw
    fi

    cargo run {{_target_triple}}

[no-cd]
@format:
    cargo fmt

[no-cd]
@lint:
    cargo clippy

[no-cd]
@lint-fix:
    cargo clippy --fix --allow-dirty

[no-cd]
@ci:
    cargo fmt --check
    cargo clippy

[no-cd]
@test:
    cargo test

@clean:
    find . -name 'Cargo.toml' -printf "%P\\n" \
        | xargs dirname \
        | xargs -I+ sh -c "(cd + && echo {{RED}}+ && cargo clean)"
